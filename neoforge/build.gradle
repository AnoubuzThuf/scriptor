plugins {
  id "com.github.johnrengelman.shadow"
}

architectury {
  platformSetupLoomIde()
  neoForge()
}

configurations {
  common {
    canBeResolved = true
    canBeConsumed = false
  }
  compileClasspath.extendsFrom common
  runtimeClasspath.extendsFrom common
  developmentNeoForge.extendsFrom common

  archivesBaseName = rootProject.archives_base_name + "-neoforge"

  // Files in this configuration will be bundled into your mod using the Shadow plugin.
  // Don't use the `shadow` configuration from the plugin itself as it's meant for excluding files.
  shadowBundle {
    canBeResolved = true
    canBeConsumed = false
  }
}

repositories {
  maven {
    name = 'NeoForged'
    url = 'https://maven.neoforged.net/releases'
  }
  maven {
    name = "Kotlin for Forge"
    setUrl("https://thedarkcolour.github.io/KotlinForForge/")
  }
  mavenCentral()
}

dependencies {
  neoForge "net.neoforged:neoforge:$rootProject.neoforge_version"
  implementation("thedarkcolour:kotlinforforge-neoforge:$rootProject.kff_version") { transitive false }

  modImplementation("com.ssblur.unfocused:unfocused-neoforge:${rootProject.unfocused_version}")

  common(project(path: ':common', configuration: 'namedElements')) { transitive false }
  shadowBundle project(path: ':common', configuration: 'transformProductionNeoForge')

  runtimeOnly "dev.emi:emi-neoforge:${emi_version}"
}

processResources {
  inputs.property 'version', project.version

  filesMatching('META-INF/neoforge.mods.toml') {
    expand(
            version: project.version,
            unfocused_version: rootProject.unfocused_version
    )
  }
}

shadowJar {
  configurations = [project.configurations.shadowBundle]
  archiveClassifier = 'dev-shadow'
}

remapJar {
  input.set shadowJar.archiveFile
}

loom {
  accessWidenerPath = project(":common").loom.accessWidenerPath
}

kotlin {
  jvmToolchain(21)
}